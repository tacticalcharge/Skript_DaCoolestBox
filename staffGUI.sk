command /staffgui [<player>]:
	aliases: staff, st
	permission: staff.use
	trigger:
	# decide target first, then check online
		if arg-1 is set:
			if arg-1 is not online:
				send "&cThat player is offline." to player
				stop
			set {_target} to arg-1
		else:
			set {_target} to player
			
		create a gui with virtual chest inventory with 3 rows named "&cStaff Tools: %{_target}%":
			make gui slot 3 with barrier named "&cKick":
				close player's inventory
				set {staff.action.%player's uuid%} to "kick"
				set {staff.target.%player's uuid%} to {_target}
				send "&cType the kick reason in chat."
				send "&cType &e'cancel' &cto cancel."

			make gui slot 5 with redstone block named "&4Ban":
				close player's inventory
				set {staff.action.%player's uuid%} to "ban"
				set {staff.target.%player's uuid%} to {_target}
				send "&cType the ban reason in chat."
				send "&cType &e'cancel' &cto cancel."

			make gui slot 11 with chest named "&aInvsee":
				open inventory of {_target} to player
				
			make gui slot 15 with ender chest named "&5Enderchest":
				open ender chest of {_target} to player
				
			make gui slot 13 with ender pearl named "&dTeleport":
				teleport player to {_target}
				
			make gui slot 19 with emerald named "&aGamemode" with lore "&bCurrent gamemode: %gamemode of {_target}%":
				if gamemode of {_target} is survival:
					set gamemode of {_target} to creative
					send "&aSet %{_target}% to Creative mode." to player
					if {_target} is not player:
						send "&aYour gamemode has been set to Creative mode by a staff member." to {_target}
				else:
					set gamemode of {_target} to survival
					send "&aSet %{_target}% to Survival mode." to player
					if {_target} is not player:
						send "&aYour gamemode has been set to Survival mode by a staff member." to {_target}
               # refresh it to show the right gamemode 
				close player's inventory
				wait 2 ticks
				execute player command "/staffgui %{_target}%"
			make gui slot 21 with golden apple named "&aHeal":
				set health of {_target} to max health of {_target}
				if {_target} is not player:
					send "&aYou have been healed by a staff member." to {_target}
				send "&aHealed %{_target}%." to player
			make gui slot 23 with cooked beef named "&6Feed":
				set food level of {_target} to 20
				if {_target} is not player:
					send "&aYou have been fed by a staff member." to {_target}
				send "&aFed %{_target}%." to player
				
			make gui slot 25 with lava bucket named "&6Clear Inventory":
				create a gui with virtual chest inventory with 1 row named "&cConfirm clear inventory?":
					make gui slot 2 with green concrete named "&aYes":
						clear inventory of {_target}
						if {_target} is not player:
							send "&aYour inventory has been cleared by a staff member." to {_target}
						send "&aCleared inventory of %{_target}%." to player
						close player's inventory
					make gui slot 6 with red concrete named "&cNo":
						close player's inventory
						wait 1 tick
						execute player command "/staffgui %{_target}%"
				open last gui to player
		open last gui to player


on chat:
	if {staff.action.%player's uuid%} is not set:
		stop

	cancel event
	set {_msg} to message

	if {_msg} is "cancel":
		delete {staff.action.%player's uuid%}
		delete {staff.target.%player's uuid%}
		send "&aAction cancelled."
		stop

	set {_target} to {staff.target.%player's uuid%}
	if {_target} is not online:
		send "&cThat player is no longer online."
		stop

	if {staff.action.%player's uuid%} is "kick":
		kick {_target} due to {_msg}
		send "&aKicked %{_target}% for: %{_msg}%"

	else if {staff.action.%player's uuid%} is "ban":
		ban {_target} due to {_msg}
		kick {_target} due to "You have been banned: %{_msg}%"
		send "&aBanned %{_target}% for: %{_msg}%"

	delete {staff.action.%player's uuid%}
	delete {staff.target.%player's uuid%}
